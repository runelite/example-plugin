plugins {
    id 'java'
    id 'maven-publish'  // Add this line
    id 'org.jetbrains.kotlin.jvm'
}

repositories {
    mavenLocal()
    maven {
        url = 'https://repo.runelite.net'
        content { includeGroupByRegex("net\\.runelite.*") }
    }
    mavenCentral()
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
}

def runeLiteVersion = 'latest.release'

dependencies {
    compileOnly group: 'net.runelite', name:'client', version: runeLiteVersion

    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    testImplementation 'junit:junit:4.12'
    testImplementation group: 'net.runelite', name:'client', version: runeLiteVersion
    testImplementation group: 'net.runelite', name:'jshell', version: runeLiteVersion
    implementation 'org.slf4j:slf4j-api:1.7.36'
    implementation 'io.reactivex.rxjava3:rxjava:3.1.8'
    implementation 'com.ethanvann:packetutils:5.4-SNAPSHOT'

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.10.2"
    testRuntimeOnly  "org.junit.jupiter:junit-jupiter-engine:5.10.2"
    testRuntimeOnly  "org.junit.platform:junit-platform-launcher:1.10.2"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

tasks.test {
    useJUnitPlatform()
}

group = 'com.example'
version = '1.0-SNAPSHOT'

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release.set(11)
}

tasks.register('shadowJar', Jar) {
    dependsOn configurations.testRuntimeClasspath
    manifest {
        attributes('Main-Class': 'com.example.ExamplePluginTest', 'Multi-Release': true)
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from sourceSets.main.output
    from sourceSets.test.output
    from {
        configurations.testRuntimeClasspath.collect { file ->
            file.isDirectory() ? file : zipTree(file)
        }
    }

    exclude 'META-INF/INDEX.LIST'
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    exclude '**/module-info.class'

    group = BasePlugin.BUILD_GROUP
    archiveClassifier.set('shadow')
    archiveFileName.set("${rootProject.name}-${project.version}-all.jar")
}

kotlin {
    jvmToolchain(11)
}